plugins {
    id 'org.springframework.boot' version '3.4.3'
    id 'io.spring.dependency-management' version '1.1.7'
}

allprojects {
    repositories {
        mavenCentral()
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'checkstyle'

    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(21)
        }
    }

    dependencies {
        implementation 'org.springframework.boot:spring-boot-starter-web'
    }

    checkstyle {
        toolVersion = '10.12.1'
        configFile = rootProject.file('config/checkstyle/naver-checkstyle-rules.xml')
        configProperties = ['suppressionFile': "${rootDir}/config/checkstyle/naver-checkstyle-suppressions.xml"]
        ignoreFailures = false
        showViolations = true
    }

    tasks.withType(Checkstyle).tap {
        configureEach {
            reports {
                xml.required = false
                html.required = true
            }
        }
    }
}
